apply plugin: 'com.android.application'

android {
    signingConfigs {
        sign
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ouo.pixivmuzei"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 11
        versionName "1.3.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def fileName = "PixivMuzei_${variant.versionName}.apk"
                    outputFileName = fileName
                }
            }

            signingConfig signingConfigs.sign
        }
        debug {
            debuggable true
        }
    }
}

def ksFile = rootProject.file('keystore.properties')
def props = new Properties();
if (ksFile.canRead()) {
    props.load(new FileInputStream(ksFile))

    if (props != null) {
        android.signingConfigs.sign.storeFile file(props['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword props['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias props['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword props['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'keystore.properties\' not found!'
    }
} else {
    println '\'keystore.properties\' not found!'
}

dependencies {
    //    compile fileTree(include: ['*.jar'], dir: 'libs')
    //    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    //        exclude group: 'com.android.support', module: 'support-annotations'
    //    })
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.github.hotchemi:permissionsdispatcher:2.0.7'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.0.7'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'
    implementation 'com.google.android.apps.muzei:muzei-api:3.1.0'
}